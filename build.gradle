buildscript {
    ext {
        springBootVersion = "2.1.3.RELEASE"
        kotlinVersion = "1.3.30"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    group "org.mashup.backend.spring-message"

    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "kotlin-spring"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    repositories {
        mavenCentral()
    }
}

subprojects {
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.springframework.boot:spring-boot-starter"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
}

project(":common") {
    version "0.0.1-SNAPSHOT"

    apply plugin: "kotlin-jpa"

    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-data-jpa"
        implementation "org.springframework.boot:spring-boot-starter-jdbc"
        runtimeOnly "com.h2database:h2"
        runtimeOnly "mysql:mysql-connector-java"
    }

    allOpen {
        annotation "javax.persistence.Entity"
    }
}

project(":producer") {
    version "0.0.1-SNAPSHOT"

    bootJar.enabled = true
    jar.enabled = false

    dependencies {
        implementation project(":common")
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    }
}

project(":consumer") {
    version "0.0.1-SNAPSHOT"

    bootJar.enabled = true
    jar.enabled = false

    dependencies {
        implementation project(":common")
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    }
}

project(":apigw") {
    version "0.0.1-SNAPSHOT"

    bootJar.enabled = true
    jar.enabled = false

    dependencies {
        implementation project(":common")
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    }
}